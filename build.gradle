plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.asciidoctor.convert' version '1.5.6'
}

group = 'com.kustacks'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'junit:junit:4.13.1'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.5.RELEASE'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.5.RELEASE'

	implementation 'org.springframework.boot:spring-boot-starter-validation'
}

ext {
	snippetsDir = file 'build/generated-snippets' // (2)
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	attributes "snippets": snippetsDir,
			"version": version,
			"stylesheet": "asciitheme/clean.css"
	inputs.dir snippetsDir
	dependsOn test
}

asciidoctor.doFirst {
	println "=====start asciidoctor"
	//asciidoctor 실행전 기존에 생성된 API 문서 삭제
	delete file('src/main/resources/static/docs')
}
asciidoctor.doLast {
	println "=====finish asciidoctor"
}

task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/asciidoc/html5")
	// resources/static/docs 로 복사하여 서버가 돌아가고 있을때 /docs/index.html 로 접속하면 볼수 있음
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	enabled = true
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}/html5") {
		into "static/docs"
	}
	manifest {
		attributes 'Main-Class': 'com.kustacks.kuring.KuringApplication'
	}
}

// bootJar로 manifest 옮김
// 혹시 배포 안되면 여기 체크
jar.enabled = false